apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

description = "Sparkling Water Assembly"

dependencies {
  // Sparkling Water Core
  compile( project(":sparkling-water-core") ) { 
    exclude group: "javax.servlet", module: "servlet-api"
  }
  compile( project(":sparkling-water-examples") )

  compile (project(":sparkling-water-ml"))

  // And Scala library
  compile "org.scala-lang:scala-library:${scalaVersion}" 
}

jar {
  enabled = false // we do not need to generate jar file in this case
  baseName = "${jar.baseName}_${scalaBaseVersion}"
}

// Remove the jar artifacts to satisfy release plugin
configurations.archives.artifacts.removeAll { it.archiveTask.is(jar) }

// Shadow jar settings
shadowJar {
  mergeServiceFiles()
  relocate 'javassist', 'ai.h2o.javassist'
  // Relocate Eclipse Jetty to avoid collisions with Spark Jetty (newer version)
  relocate 'org.eclipse.jetty', 'ai.h2o.org.eclipse.jetty'
  dependencies {
    // This has to be specific list of dependencies
    // Do not forget that Sparkling Water is intended 
    // to run inside Spark environment providing some jars 
    // (like hadoop, aws) on classpath
    //
    include(project(":sparkling-water-core"))
    include(project(":sparkling-water-examples"))
    include(project(":sparkling-water-repl"))
    include(project(":sparkling-water-ml"))
    include(dependency("ai.h2o:h2o-genmodel"))
    include(dependency("ai.h2o:h2o-core"))
    include(dependency("ai.h2o:h2o-scala_${scalaBaseVersion}"))
    include(dependency("ai.h2o:h2o-web"))
    include(dependency("ai.h2o:h2o-algos"))
    include(dependency("ai.h2o:h2o-automl"))
    include(dependency("ai.h2o:h2o-persist-hdfs"))
    include(dependency("ai.h2o:h2o-persist-s3"))
    include(dependency("ai.h2o:h2o-avro-parser"))
    include(dependency("ai.h2o:h2o-orc-parser"))
    include(dependency("ai.h2o:h2o-parquet-parser"))
    include(dependency("ai.h2o:reflections"))
    include(dependency("ai.h2o:google-analytics-java"))
    include(dependency("ai.h2o:deepwater-backend-api"))
    include(dependency("ai.h2o:h2o-ext-xgboost"))
    include(dependency("ai.h2o:h2o-genmodel-ext-xgboost"))

    include(dependency("joda-time:joda-time"))
    include(dependency("org.joda:joda-convert"))
    include(dependency("org.javassist:javassist"))
    include(dependency("gov.nist.math:jama"))
    include(dependency("com.google.code.gson:gson"))
    include(dependency("com.github.tony19:named-regexp"))
    include(dependency("com.amazonaws:aws-java-sdk-s3"))
    include(dependency("com.amazonaws:aws-java-sdk-kms"))
    include(dependency("com.amazonaws:aws-java-sdk-core"))
    include(dependency("no.priv.garshol.duke:duke"))
    include(dependency("org.eclipse.jetty.aggregate:jetty-servlet"))
    include(dependency("org.eclipse.jetty:jetty-server"))
    include(dependency("org.eclipse.jetty:jetty-plus:8.1.17.v20150415"))
    baseName = "${baseName}_${scalaBaseVersion}"
  }
}

task printRSparklingCranLink(){
  doLast {
    def link = "https://CRAN.R-project.org/package=rsparkling"
    def lines = link.toURL().readLines()
    def versionIdx = lines.findIndexOf {it.equals("<td>Version:</td>")} + 1
    def version = lines.get(versionIdx).replace("<td>", "").replace("</td>", "")

    def downloadLink = "https://cran.r-project.org/src/contrib/rsparkling_${version}.tar.gz"
    println(downloadLink)
  }
}

artifacts {
  compile shadowJar
}

build.dependsOn shadowJar

