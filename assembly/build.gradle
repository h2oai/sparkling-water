apply plugin: 'java'

description = "Sparkling Water Assembly"

dependencies {
    // Sparkling Water libraries
    compile project(":sparkling-water-ml")
    compile project(":sparkling-water-repl")
    compile(project(":sparkling-water-core")) {
        exclude group: "javax.servlet", module: "servlet-api"
    }
    compile(project(":sparkling-water-extension-stack-trace"))

    // Spark libraries
    compile "org.apache.spark:spark-core_${scalaBaseVersion}:${sparkVersion}"
    compile "org.apache.spark:spark-sql_${scalaBaseVersion}:${sparkVersion}"
    compile "org.apache.spark:spark-mllib_${scalaBaseVersion}:${sparkVersion}"
    compile "org.apache.spark:spark-repl_${scalaBaseVersion}:${sparkVersion}"
}

jar {
    enabled = false // we do not need to generate jar file in this case
    archiveBaseName = "${jar.archiveBaseName.get()}_${scalaBaseVersion}"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply from: "$rootDir/gradle/shadowRuntime.gradle"

shadowJar {
    configurations = [project.configurations.shadowRuntime]

    mergeServiceFiles()

    relocate 'javassist', 'ai.h2o.javassist'
    // Relocate Eclipse Jetty to avoid collisions with Spark Jetty (newer version)
    relocate 'org.eclipse.jetty', 'ai.h2o.org.eclipse.jetty'
    relocate 'org.eclipse.jetty.orbit', 'ai.h2o.org.eclipse.jetty.orbit'
    relocate 'com.google.common', 'ai.h2o.com.google.common'

    exclude 'www/flow/packs/test-*/**'
}

artifacts {
    compile shadowJar
}

build.dependsOn shadowJar

