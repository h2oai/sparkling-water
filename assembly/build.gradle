apply plugin: 'com.github.johnrengelman.shadow'

description = "Sparkling Water Assembly"

dependencies {
  // Sparkling Water Core
  compile( project(":sparkling-water-core") ) { 
    exclude group: "javax.servlet", module: "servlet-api"
  }
  compile( project(":sparkling-water-examples") )  
  
  // Force this dependency
  runtime( "ai.h2o:h2o-flow:$h2oFlowVersion" )
}

shadowJar {
  dependencies {
    // This has to be specific list of dependencies
    // Do not forget that Sparkling Water is intended 
    // to run inside Spark environment providing some jars 
    // (like hadoop, aws) on classpath
    include(project(":sparkling-water-core"))
    include(project(":sparkling-water-examples"))
    include(dependency("ai.h2o:h2o-core"))
    include(dependency("ai.h2o:h2o-scala_${scalaBinaryVersion}"))
    include(dependency("ai.h2o:h2o-app"))
    include(dependency("ai.h2o:h2o-web"))
    include(dependency("ai.h2o:h2o-algos"))
    include(dependency("ai.h2o:h2o-flow"))
    include(dependency("joda-time:joda-time"))
    include(dependency("org.javassist:javassist"))
    include(dependency("gov.nist.math:jama"))
  }
}

artifacts {
  archives shadowJar
}
