apply plugin: 'com.github.johnrengelman.shadow'

description = "Sparkling Water Assembly"

dependencies {
  // Sparkling Water Core
  compile( project(":sparkling-water-core") ) { 
    exclude group: "javax.servlet", module: "servlet-api"
  }
  compile( project(":sparkling-water-examples") )  

  // And Scala library
  compile "org.scala-lang:scala-library:${scalaVersion}" 
}

shadowJar {
  dependencies {
    // This has to be specific list of dependencies
    // Do not forget that Sparkling Water is intended 
    // to run inside Spark environment providing some jars 
    // (like hadoop, aws) on classpath
    //
    // DO NOT Forget to update a list of jars used in testAssembly task
    include(project(":sparkling-water-core"))
    include(project(":sparkling-water-examples"))
    include(dependency("ai.h2o:h2o-core"))
    include(dependency("ai.h2o:h2o-scala_${scalaBinaryVersion}"))
    include(dependency("ai.h2o:h2o-app"))
    include(dependency("ai.h2o:h2o-web"))
    include(dependency("ai.h2o:h2o-algos"))
    include(dependency("ai.h2o:h2o-persist-hdfs"))
    include(dependency("ai.h2o:h2o-genmodel"))
    include(dependency("joda-time:joda-time"))
    include(dependency("org.joda:joda-convert"))
    include(dependency("org.javassist:javassist"))
    include(dependency("gov.nist.math:jama"))
    include(dependency("com.google.code.gson:gson"))
    include(dependency("org.reflections:reflections"))
    include(dependency("ai.h2o:google-analytics-java"))
    include(dependency("com.google.guava:guava"))
    include(dependency("com.github.tony19:named-regexp"))
  }
}

artifacts {
  archives shadowJar
}
