apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

description = "Sparkling Water Assembly"

dependencies {
  // Sparkling Water Core
  compile( project(":sparkling-water-core") ) { 
    exclude group: "javax.servlet", module: "servlet-api"
  }
  compile( project(":sparkling-water-examples") )  

  // And Scala library
  compile "org.scala-lang:scala-library:${scalaVersion}" 
}

shadowJar {
  relocate 'javassist', 'ai.h2o.javassist'
  dependencies {
    // This has to be specific list of dependencies
    // Do not forget that Sparkling Water is intended 
    // to run inside Spark environment providing some jars 
    // (like hadoop, aws) on classpath
    //
    include(project(":sparkling-water-core"))
    include(project(":sparkling-water-examples"))
    include(project(":sparkling-water-repl"))
    include(dependency("ai.h2o:h2o-core"))
    include(dependency("ai.h2o:h2o-scala_${scalaBinaryVersion}"))
    include(dependency("ai.h2o:h2o-app"))
    include(dependency("ai.h2o:h2o-web"))
    include(dependency("ai.h2o:h2o-algos"))
    include(dependency("ai.h2o:h2o-persist-hdfs"))
    include(dependency("ai.h2o:h2o-genmodel"))
    include(dependency("joda-time:joda-time"))
    include(dependency("org.joda:joda-convert"))
    include(dependency("org.javassist:javassist"))
    include(dependency("gov.nist.math:jama"))
    include(dependency("com.google.code.gson:gson"))
    include(dependency("com.google.guava:guava"))
    include(dependency("ai.h2o:reflections"))
    include(dependency("ai.h2o:google-analytics-java"))
    include(dependency("com.github.tony19:named-regexp"))

    include(dependency("org.eclipse.jetty.aggregate:jetty-servlet"))
    include(dependency("org.eclipse.jetty:jetty-server"))
    include(dependency("org.eclipse.jetty:jetty-plus:8.1.17.v20150415"))
  }
}

artifacts {
  archives shadowJar
}
