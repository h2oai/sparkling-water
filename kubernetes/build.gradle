apply plugin: 'base'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile

description = "Build docker images for Kubernetes with Sparkling Water"

ext {
    outputFilePython = file("$buildDir/Dockerfile-Python")
    outputFileR = file("$buildDir/Dockerfile-R")
    outputFileScala = file("$buildDir/Dockerfile-Scala")
}

task createScalaDockerfile(type: Dockerfile) {
    destFile = outputFileScala
    from "spark:${sparkVersion}"
    copyFile("sparkling-water-assembly_2.11-${version}-all.jar", "/opt/spark/jars/sparkling-water-assembly_2.11-${version}-all.jar")
}

task createRDockerfile(type: Dockerfile) {
    destFile = outputFileR
    from "spark-r:${sparkVersion}"
    runCommand "apk add libc-dev linux-headers g++ libxml2-dev"
    runCommand """\\
                R -e 'install.packages("xml2", repos = "http://cran.us.r-project.org")' && \\
                R -e 'install.packages("openssl", repos = "http://cran.us.r-project.org")' && \\
                R -e 'install.packages("httr", repos = "http://cran.us.r-project.org")' && \\
                R -e 'install.packages("bitops", repos = "http://cran.us.r-project.org")' && \\
                R -e 'install.packages("RCurl", repos = "http://cran.us.r-project.org")' && \\
                R -e 'install.packages("jsonlite", repos = "http://cran.us.r-project.org")' && \\
                R -e 'install.packages("testthat", repos = "http://cran.us.r-project.org")' && \\
                R -e 'install.packages("sparklyr", repos = "http://cran.us.r-project.org")'
                """
    runCommand "R -e \"install.packages('h2o', type = 'source', repos = 'http://h2o-release.s3.amazonaws.com/h2o/rel-${h2oMajorName}/${h2oBuild}/R')\""
    copyFile("rsparkling_${version}.tar.gz", "/opt/spark/R/lib/rsparkling_${version}.tar.gz")
    runCommand "cd /opt/spark/R/lib && R CMD INSTALL rsparkling_${version}.tar.gz"
}

task createPythonDockerfile(type: Dockerfile) {
    destFile = outputFilePython
    from "spark-py:${sparkVersion}"
    copyFile("h2o_pysparkling_${sparkMajorVersion}-${version}.zip", "/opt/spark/pyspark/python/lib/h2o_pysparkling_${sparkMajorVersion}-${version}.zip")
    runCommand "pip install /opt/spark/pyspark/python/lib/h2o_pysparkling_${sparkMajorVersion}-${version}.zip"
}

task buildDockerfiles(dependsOn: [
    createPythonDockerfile,
    createRDockerfile,
    createScalaDockerfile
])
