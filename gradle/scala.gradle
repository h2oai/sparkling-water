apply plugin: 'scala'
apply from: "$rootDir/gradle/utils.gradle"

// Activate Zinc compiler and configure scalac
tasks.withType(ScalaCompile) {
    scalaCompileOptions.useCompileDaemon = false
    scalaCompileOptions.useAnt = false
    scalaCompileOptions.additionalParameters = ['-target:jvm-1.6']
}

project.archivesBaseName = "${project.name}_${scalaBaseVersion}"

project.tasks.withType(AbstractArchiveTask) { AbstractArchiveTask task ->
    task.baseName = "${project.name}_${scalaBaseVersion}"
}

// Create jar
task testJar(type: Jar, dependsOn: testClasses) {
    group = "Build"
    description = "Assembles a jar archive with test classes."
    baseName = "${project.name}_${scalaBaseVersion}"
    appendix = 'test'
    from sourceSets.test.output
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'scaladoc'
    from scaladoc
}

// Create a configuration containing only for test artifacts
configurations {
    testArchives
}

// Explicitly
artifacts {
    testArchives testJar
    archives scaladocJar
}

test {
    maxParallelForks = 1
    // Also setup expected Scala version for Spark launcher
    environment "SPARK_SCALA_VERSION", "$scalaBaseVersion"
}

// add additional source set for desired scala version
sourceSets {
    main {
        scala {
            srcDir "src/main/scala_$scalaBaseVersion"
        }
    }
    test {
        scala {
            srcDirs "src/test/scala_$scalaBaseVersion"
        }
    }
}

// Enable scalastyle
apply from: "$rootDir/gradle/scalastyle.gradle"

