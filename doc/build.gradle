description = "Sparkling Water Documentation"

import static groovy.io.FileType.FILES

task cleanDoc(type: Delete) {
    delete getBuildDir()
}

task createVersionFile {
    doLast {
        if (!buildDir.exists()) {
            buildDir.mkdir()
        }
        File version_file = new File(buildDir, "project_version")
        def version_txt = version.replace("-SNAPSHOT", "")
        version_file.write(version_txt)
    }
}

/**
 * We can't use Sphinx substitutions in for example code blocks and other container, so we post-process
 * the generated files manually
 */
task substitute() {
    doLast {
        def siteDir = "${buildDir}/site"

        def (first, second, third) = version.tokenize(".")
        def majorVersion = "${first}.${second}"
        def minorVersion = third.replace("-SNAPSHOT", "")

        new File(siteDir).eachFileRecurse(FILES) {
            if (it.name.endsWith('.html')) {
                def contents = file(it).getText('UTF-8')
                contents = contents
                        .replaceAll("SUBST_SW_VERSION", version.replace("-SNAPSHOT", ""))
                        .replaceAll("SUBST_SW_MAJOR_VERSION", majorVersion)
                        .replaceAll("SUBST_SW_MINOR_VERSION", minorVersion)
                        .replaceAll("SUBST_SPARK_VERSION", sparkVersion)
                        .replaceAll("SUBST_SPARK_MAJOR_VERSION", sparkMajorVersion)
                        .replaceAll("SUBST_H2O_VERSION", h2oVersion)
                        .replaceAll("SUBST_H2O_RELEASE_NAME", h2oMajorName)
                        .replaceAll("SUBST_H2O_BUILD_NUMBER", h2oBuild)
                        .replaceAll("SUBST_MIN_SUPPORTED_JAVA", minSupportedJavaVersion)
                file(it).write(contents, 'UTF-8')
            }
        }
    }
}


task docJar(type: Jar) {
    from "${buildDir}"
    classifier = "site"
    include "site/**"
    baseName = "${project.name}_${scalaBaseVersion}"
}

task sourcesDocJar(type: Jar) {
    classifier = "site-sources"
    from "${projectDir}"
    include "src/**"
    baseName = "${project.name}_${scalaBaseVersion}"


    from "${projectDir}"
    include "CHANGELOG.rst"
}

task openSite {
    doFirst {
        def sitePath = "file://" + project.buildDir.toString() + "/site/index.html"
        if (file(sitePath).exists()) {
            java.awt.Desktop.desktop.browse sitePath.toURI()
        } else {
            throw new IllegalArgumentException("Documentation is not available, please build it first by running ./gradlew site")
        }
    }
}


artifacts {
    publishArchives docJar
    publishArchives sourcesDocJar
}

publish.dependsOn site
createVersionFile.dependsOn cleanDoc
sphinx.dependsOn createVersionFile
clean.dependsOn cleanDoc
substitute.dependsOn sphinx
sourcesDocJar.dependsOn substitute
docJar.dependsOn sourcesDocJar
site.dependsOn docJar

defineStandardPublication().call()
