apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java-library'

description = "Sparkling Water Extensions Assembly"

dependencies {
  api("org.scala-lang:scala-library:${scalaVersion}")
  api(project(":sparkling-water-extensions"))
  api(project(":sparkling-water-utils")) { transitive = false }
}

jar {
  enabled = false // we do not need to generate jar file in this case
}

shadowJar {
  mergeServiceFiles()

  archiveBaseName = "${archiveBaseName.get()}_${scalaBaseVersion}"
}

artifacts {
  api shadowJar
}

task embeddedAssemblyJar(type: Copy, dependsOn: shadowJar) {
  duplicatesStrategy = 'include'
  from file("$buildDir/libs")
  into file("$buildDir/embedded")
  rename { _ -> 'assembly-extensions.jar.embedded' }
}

build.dependsOn embeddedAssemblyJar
