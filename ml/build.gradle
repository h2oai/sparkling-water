description = "Sparkling Water ML Pipelines"

apply from: "$rootDir/gradle/sparkTest.gradle"
apply from: "$rootDir/gradle/utils.gradle"

dependencies {
    // Sparkling Water Core
    api(project(":sparkling-water-core")) {
        exclude group: "javax.servlet", module: "servlet-api"
    }

    api project(":sparkling-water-scoring")
    // MLLIb pipelines
    compileOnly "org.apache.spark:spark-mllib_${scalaBaseVersion}:${sparkVersion}"
    testImplementation "org.apache.spark:spark-mllib_${scalaBaseVersion}:${sparkVersion}"

    // Dependency for Target Encoder
    api "ai.h2o:h2o-ext-target-encoder:${h2oVersion}"
    testImplementation "ai.h2o:h2o-ext-target-encoder:${h2oVersion}"

    // And Scala library
    api "org.scala-lang:scala-library:${scalaVersion}"

    testImplementation "org.apache.spark:spark-repl_${scalaBaseVersion}:${sparkVersion}"


    api "ai.h2o:mojo2-runtime-api:${mojoPipelineVersion}"
    api "ai.h2o:mojo2-runtime-impl:${mojoPipelineVersion}"

    testImplementation "ai.h2o:mojo2-runtime-api:${mojoPipelineVersion}"
    testImplementation "ai.h2o:mojo2-runtime-impl:${mojoPipelineVersion}"

    // And use scalatest for Scala testing
    testImplementation "org.scalatest:scalatest_${scalaBaseVersion}:2.2.1"
    testImplementation "junit:junit:4.11"
    testImplementation project(path: ':sparkling-water-core', configuration: 'testArchives')

    // to reuse some utils test classes defined in the core
    testImplementation project(path: ':sparkling-water-core', configuration: 'testArchives')

    // For test purposes, force newer client as the tests are modifying classpath and we get errors which
    // we do not get during normal production usage
    testImplementation('org.apache.hadoop:hadoop-mapreduce-client-core') {
        version {
            strictly '2.7.2'
        }
    }
    testImplementation('org.apache.hadoop:hadoop-common:2.7.2') {
        version {
            strictly '2.7.2'
        }
    }
    // required for org.apache.hadoop.util.StopWatch
    testImplementation('commons-io:commons-io:2.4') {
        version {
            strictly '2.4'
        }
    }
    // required for org.apache.commons.io.Charsets that is used internally

    compileOnly project(":sparkling-water-macros")
}


test {
    if (project.property("testMojoPipeline") != "true") {
        exclude 'org/apache/spark/ml/spark/models/H2OMOJOPipelineModelTest.class'
    }
}

defineStandardPublication().call()
