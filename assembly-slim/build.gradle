apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java-library'
apply from: "$rootDir/gradle/assembly.gradle"

description = "Sparkling Water Assembly Slim (for pypi)"

dependencies {
  api(project(":sparkling-water-ml"))
  api(project(":sparkling-water-repl"))
  api(project(":sparkling-water-core"))
  api(project(":sparkling-water-extensions"))
}

configurations {
  slimShadowApi {
    extendsFrom shadowApi
    exclude group: "ai.h2o", module: "h2o-ext-jython-cfunc"
    exclude group: "ai.h2o", module: "h2o-ext-xgboost"
  }
}

shadowJar {
  configurations = [project.configurations.slimShadowApi]
  mergeServiceFiles()

  relocate 'javassist', 'ai.h2o.javassist'
  relocate 'com.google.common', 'ai.h2o.com.google.common'
  relocate 'org.eclipse.jetty', 'ai.h2o.org.eclipse.jetty'
  relocate 'org.eclipse.jetty.orbit', 'ai.h2o.org.eclipse.jetty.orbit'
  relocate 'scala.compat.java8', 'ai.h2o.scala.compat.java8'
  relocate 'scala.concurrent.java8', 'ai.h2o.scala.concurrent.java8'
  relocate 'com.amazonaws', 'ai.h2o.com.amazonaws'
  relocate 'org.apache.http', 'ai.h2o.org.apache.http'
  from "$project.buildDir/reports/" include '**/*'
  exclude 'www/flow/packs/test-*/**'

  archiveBaseName = "${archiveBaseName.get()}_${scalaBaseVersion}"
}

artifacts {
  api shadowJar
}

build.dependsOn shadowJar
