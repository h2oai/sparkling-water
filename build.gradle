//
// Sparkling Water build file.
// This is a multi-module build file.
//
apply plugin: 'base'

// For multiproject setup we have to apply release plugin here (we share same release number cross all modules)
if (project.hasProperty("doRelease")) {
    apply from: 'gradle/release.gradle'
}

// The build script settings to fetch plugins and put them on
// classpath
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://dl.bintray.com/releashaus/release" }
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
        classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.10:0.8.2'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath 'com.bmuschko:gradle-docker-plugin:2.6.5'
        classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
        classpath 'kr.motd.gradle:sphinx-gradle-plugin:1.0.6'
    }
}

//
// Common configuration
//
ext {
    // Published projects
    publishedProjects = [
      project(':sparkling-water-repl'),
      project(':sparkling-water-core'),
      project(':sparkling-water-examples'),
      project(':sparkling-water-ml'),
      project(':sparkling-water-package'),
      project(':sparkling-water-doc')
    ]
    // Project with integration tests
    integTestProjects = [
      project(':sparkling-water-core'),
      project(':sparkling-water-examples')
    ]
    // Scala projects
    scalaProjects = [
      project(':sparkling-water-repl'),
      project(':sparkling-water-core'),
      project(':sparkling-water-examples'),
      project(':sparkling-water-ml'),
      project(':sparkling-water-app-streaming'),
      project(':sparkling-water-package')
    ]
    // Python projects
    pythonProjects = [
      project(':sparkling-water-py')
    ]
    docProjects = [
      project(':sparkling-water-doc')
    ]

    defaultScalaBaseVersion = '2.11'
    supportedScalaBaseVersions = ['2.10' : '2.10.6',
                                  '2.11' : '2.11.8']
}

//
// For all projects (this and all subprojects) specify common properties and tasks
//
configure(allprojects) { project ->
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply from: "$rootDir/gradle/artifacts.gradle"
    apply plugin: 'nebula.lint'
    gradleLint.rules = ['all-dependency']
    gradleLint.alwaysRun = false // Run linter manually

    // Version of main components
    ext {
      scalaBaseVersion   = project.findProperty('scalaBaseVersion') ?: defaultScalaBaseVersion
      scalaVersion       = supportedScalaBaseVersions[scalaBaseVersion]

      // h2oBuild property is defined in gradle.properties
      h2oVersion         = "$h2oMajorVersion.$h2oBuild"
      sparkVersion       = "$sparkVersion"
      junitVersion       = '4.11'
    }
}

// 
// Common configuration for all subprojects
//
configure(subprojects) { project ->
    // All project inherits the same versioning number
    version = rootProject.version

    repositories {
        // Should be enabled only in development mode
        if (h2oBuild == '99999' || project.hasProperty('useMavenLocal') || h2oBuild.endsWith("-SNAPSHOT")) {
            mavenLocal()
        }

        mavenCentral()

        // Public Sonatype repository
        maven {
            url "https://oss.sonatype.org/content/repositories/releases/"
        }

        // Snapshot repository of H2O builds
        maven {
            url "http://h2o-release.s3.amazonaws.com/h2o/master/$h2oBuild/maven/repo/"
        }
    }

    if (project in scalaProjects) {
      apply from: "$rootDir/gradle/scala.gradle"
    }
    if (project in pythonProjects) {
      apply from: "$rootDir/gradle/python.gradle"
    }

    // All subprojects needs Spark support
    apply from: "$rootDir/gradle/spark.gradle"

    // Publish artifacts
    if (project in publishedProjects) {
        apply from: "$rootDir/gradle/publish.gradle"
    }

    if (project in integTestProjects) {
        apply from: "$rootDir/gradle/itest.gradle"
    }

    if (project in docProjects){
        apply plugin: 'base'
        apply plugin: 'java'
        apply plugin: 'kr.motd.sphinx'
    }
}


task buildSparklingWaterDist(type: Exec) {
    def buildTimeMillis = System.currentTimeMillis();
    def buildTimeIso8601 = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))
    def buildTimeLocal = new Date()

    environment['BUILD_TIME_MILLIS'] = buildTimeMillis
    environment['BUILD_TIME_ISO8601'] = buildTimeIso8601
    environment['BUILD_TIME_LOCAL'] = buildTimeLocal

    commandLine './make-dist.sh'
}

task dist(dependsOn: buildSparklingWaterDist)

task wrapper(type: Wrapper) {
  gradleVersion = '4.5'
}

clean.doFirst {
  delete file("build/")
  delete file("h2ologs/")
  delete file("null/")
  delete file("metastore_db/")
}

