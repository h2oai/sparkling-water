description = "Sparkling Water Benchmarks"

apply plugin: 'com.github.johnrengelman.shadow'
apply from: "$rootDir/gradle/shadowRuntime.gradle"
apply from: "$rootDir/gradle/utils.gradle"

dependencies {
    compileOnly project(":sparkling-water-assembly-h2o")

    // Sparkling Water libraries
    compile project(":sparkling-water-ml")
    compile project(":sparkling-water-repl")
    compile(project(":sparkling-water-core")) {
        exclude group: "javax.servlet", module: "servlet-api"
    }

    // Spark libraries
    compile "org.apache.spark:spark-core_${scalaBaseVersion}:${sparkVersion}"
    compile "org.apache.spark:spark-sql_${scalaBaseVersion}:${sparkVersion}"
    compile "org.apache.spark:spark-mllib_${scalaBaseVersion}:${sparkVersion}"
    compile "org.apache.spark:spark-repl_${scalaBaseVersion}:${sparkVersion}"
}

shadowJar {
    configurations = [project.configurations.shadowRuntime]

    mergeServiceFiles()

    relocate 'javassist', 'ai.h2o.javassist'
    relocate 'com.google.common', 'ai.h2o.com.google.common'
    relocate 'org.eclipse.jetty', 'ai.h2o.org.eclipse.jetty'
    relocate 'org.eclipse.jetty.orbit', 'ai.h2o.org.eclipse.jetty.orbit'

    from "$project.buildDir/resources/main" include '**/*'
    exclude 'www/flow/packs/test-*/**'
}

task substituteTerraform() {
    doLast {
        def tfBaseDir = "${project.buildDir.toString()}/terraform/aws/"
        def tfScripts = ["${tfBaseDir}/variables.tf", "${tfBaseDir}/modules/emr/variables.tf", "${tfBaseDir}/modules/emr/main.tf"]
        tfScripts.each { path ->
            def contents = file(path).getText('UTF-8')
            contents = contents
                .replaceAll(
                    "SUBST_H2O_JAR_FILE",
                    "${project(":sparkling-water-assembly-h2o").buildDir.toString()}/libs/sparkling-water-assembly-h2o-${version}.jar")
                .replaceAll(
                    "SUBST_PACKAGE_FILE", 
                    "${project.buildDir.toString()}/libs/sparkling-water-benchmarks_$scalaBaseVersion-$version-all.jar")
                .replaceAll("SUBST_SW_VERSION", version)
                .replaceAll("SUBST_SCALA_VERSION", scalaBaseVersion)
                .replaceAll("SUBST_EMR_VERSION", supportedEmrVersion)
            
            file(path).write(contents, 'UTF-8')
        }
    }
}

task copyTerraform(type: Copy) {
    from 'src/main/terraform'
    include "**/*.tf"
    into "build/terraform"
}

task cleanTerraform(type: Delete) {
    delete "build/terraform"
}

task cleanOutput(type: Delete) {
    delete "output"
}

task runBenchmarks{
    doLast {
        exec {
            def accessKey = project.property("aws_access_key")
            def secretKey = project.property("aws_secret_key")
            def publicKey = project.property("aws_ssh_public_key")

            environment("aws_access_key", accessKey)
            environment("aws_secret_key", secretKey)
            environment("aws_ssh_public_key", publicKey)

            commandLine "./run_benchmarks.sh"
        }
    }
}

copyTerraform.dependsOn cleanTerraform
substituteTerraform.dependsOn copyTerraform
build.dependsOn substituteTerraform
build.dependsOn shadowJar
runBenchmarks.dependsOn build
runBenchmarks.dependsOn cleanOutput
